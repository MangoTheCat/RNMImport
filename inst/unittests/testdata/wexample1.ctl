;Model Desc: Two compartment Model, Using ADVAN3, TRANS4
;Project Name: wexamples
;Project ID: NO PROJECT DESCRIPTION

$PROBLEM RUN# wexample1
$INPUT C SET ID JID TIME  DV AMT RATE EVID MDV CMT
$DATA     wexample1.csv IGNORE=@
$SUBROUTINES ADVAN3 TRANS4
$PK
MU_1=THETA(1)
MU_2=THETA(2)
MU_3=THETA(3)
MU_4=THETA(4)
CL=EXP(MU_1+ETA(1))
V1=EXP(MU_2+ETA(2))
Q=EXP(MU_3+ETA(3))
V2=EXP(MU_4+ETA(4))
S1=V1

$ERROR
Y = F + F*EPS(1)

; Initial values of THETA
$THETA 
(0.001, 2.0) ;[LN(CL)]
(0.001, 2.0) ;[LN(V1)]
(0.001, 2.0) ;[LN(Q)]
(0.001, 2.0) ;[LN(V2)]
;INITIAL values of OMEGA
$OMEGA BLOCK(4)
0.4   ;[P]
0.001  ;[F]
0.4   ;[P]
0.001  ;[F]
0.001  ;[F]
0.4   ;[P]
0.001  ;[F]
0.001  ;[F]
0.001  ;[F]
0.4   ;[P]
;Initial value of SIGMA
$SIGMA 
(0.6 )   ;[P]

$EST METHOD=ITS INTERACTION FILE=wexample1.EXT CTYPE=3
     NITER=1000 PRINT=5 NOABORT MSFO=wexample1.MSF NSIG=3 SIGL=6
$EST METHOD=SAEM NBURN=2000 NITER=500 PRINT=100
$EST METHOD=IMP EONLY=1 NITER=5 ISAMPLE=3000 PRINT=1
$EST METHOD=COND INTERACTION MAXEVAL=9999 NSIG=2 SIGL=10 PRINT=5 FILE=wexample1.EXT
$COV MATRIX=R
$TABLE ID TIME DV PRED RES WRES CPRED CWRES EPRED ERES EWRES NOAPPEND ONEHEADER 
 FILE=wexample1.TAB NOPRINT
$TABLE ID CL V1 Q V2 FIRSTONLY NOAPPEND NOPRINT FILE=wexample1.PAR
$TABLE ID ETA1 ETA2 ETA3 ETA4 FIRSTONLY NOAPPEND NOPRINT FILE=wexample1.ETA
